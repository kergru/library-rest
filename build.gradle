plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'org.springframework.boot' version '3.5.5' apply false
}

allprojects {
    group = 'org.kergru'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    layout.buildDirectory.set(rootProject.layout.buildDirectory.dir(project.name))

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.5"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
        }
    }


    tasks.withType(Test).configureEach {
        useJUnitPlatform()  // JUnit 5
        outputs.upToDateWhen { false } // Test Cache umgehen, damit Tests immer gefunden werden
        testLogging {
            events "passed", "skipped", "failed"
            showStandardStreams = true
        }
    }
}
